## 记忆时间

## 目录

20220305Understanding-how-dimensions-are-associated-with-objects.md

20220308AutoLisp-Reactors.md

20220310CAD二次开发介绍.md

## 20220305Understanding-how-dimensions-are-associated-with-objects.md

[Understanding how dimensions are associated with objects - AutoCAD Tips Blog](https://allaboutcad.com/understanding-how-dimensions-are-associated-with-objects/)

By Ellen Finkelstein

This is a guest post by Sanjay Kulkarni, an AutoCAD programmer.

Learning AutoLISP (or any other programming language) doesn't compel you to become a programmer. You can still use it to better understand the internal working of AutoCAD and enhance your interactive working skill. This can also give you an edge over others.

Recently I received a client's drawing that was in AutoCAD 2004. Even when I saved it in AutoCAD 2010, the dimension would not adjust when I changed the length of a line. Only after I recreated the dimension was I able to use this capability. The reason, I found, was that the dimension in AutoCAD 2004 did not have the reactor (discussed below). Only when I created the dimension in AutoCAD 2010 was the reactor added.

Let's learn this lesson with an example.

Everybody knows that when you dimension an object, the dimension automatically updates when you modify the object. Have you ever wondered how the dimension knows it has to update? AutoLISP can help you find this out, if you don't already know.

Here's the example:

1 Draw a line.

2 Get its entity list by typing the following piece of AutoLISP code at the command prompt:

```
(entget (car (entsel)))
```

and selecting the line when prompted to select an object. You'll see something similar to the following at the command line in return.

```
((-1 . <Entity name: 7ee45a18>) (0 . “LINE”) (330 . <Entity name: 7ee43cf8>) (5 . “22B”) (100 . “AcDbEntity”) (67 . 0) (410 . “Model”) (8 . “0″) (100 . “AcDbLine”) (10 10.0 10.0 0.0) (11 160.0 160.0 0.0) (210 0.0 0.0 1.0))
```

This, you might have guessed, is the record that AutoCAD stores in its database.

3 Create a dimension (aligned for example) to show the length of the line and get the entity list of the line again. You should get a result similar to:

```
((-1 . <Entity name: 7ee45a18>) (0 . “LINE”) (5 . “22B”) (102 . “{ACAD_REACTORS”) (330 . <Entity name: 7ee45ae8>) (102 . “}”) (330 . <Entity name: 7ee43cf8>) (100 . “AcDbEntity”) (67 . 0) (410 . “Model”) (8 . “0″) (100 . “AcDbLine”) (10 10.0 10.0 0.0) (11 160.0 160.0 0.0) (210 0.0 0.0 1.0))
```

You might have noticed a new item starting with `(102 . “{ACAD_REACTORS”) (330 . <Entity name: 7ee45ae8>) (102 . “}”)` in the entity list of the line.

This is a reference to a reactor object that AutoCAD has created after adding the dimension. This reactor ensures that dimension gets modified when the object (line in this case) gets modified.

A reactor is a program that executes automatically when a particular action (change in the length of line in this case) is completed. A reactor executes only AFTER an editing action is completed. Hence, you see that the dimension is modified after the command that modifies the line completes and is not dynamically updated while length of line is being modified.

4 Next, entget the dimension using the same code but selecting the dimension this time. You will get something similar to:

```
((-1 . <Entity name: 7ee45a20>) (0 . “DIMENSION”) (5 . “22C”) (102 . “{ACAD_XDICTIONARY”) (360 . <Entity name: 7ee45a90>) (102 . “}”) (102 . “{ACAD_REACTORS”) (330 . <Entity name: 7ee45ae8>) (102 . “}”) (330 . <Entity name: 7ee43cf8>) (100 . “AcDbEntity”) (67 . 0) (410 . “Model”) (8 . “0″) (100 . “AcDbDimension”) (280 . 0) (2 . “*D6″) (10 173.511 146.489 0.0) (11 98.5106 71.4894 0.0) (12 0.0 0.0 0.0) (70 . 33) (1 . “”) (71 . 5) (72 . 1) (41 . 1.0) (42 . 212.132) (73 . 0) (74 . 0) (75 . 0) (52 . 0.0) (53 . 0.0) (54 . 0.0) (51 . 0.0) (210 0.0 0.0 1.0) (3 . “Standard”) (100 . “AcDbAlignedDimension”) (13 10.0 10.0 0.0) (14 160.0 160.0 0.0) (15 0.0 0.0 0.0) (16 0.0 0.0 0.0) (40 . 0.0) (50 . 0.0))
```

You will notice that exactly same reactor appears in the entity list of dimension. Thus, this reactor is the link between the line and dimension.

5 Add one more dimension to the line and get the entity list of line once more. You will see one more item with (330 . in the reactor reference. Thus each dimension associated with line is added as an item with 330 dxf code in the entity list of line.

6 Now delete one of the dimensions and get the new entity list of the line. You will find that one of the (330 . item has been removed.

7 After you delete all dimensions, the entity list of the line returns to its original form without any (102 . items.

A possibility to improve associativity

It is interesting to note that when object is deleted, dimension does not get deleted. Thus it lacks true associativity with the object.

You could create a situation whereby the dimension would be deleted when the object was deleted by adding a reference to one more reactor in the dimension's entity list that responds to deleting its associated object, but you would have to write code to do so.

Sanjay Kulkarni is an experienced CAD (AutoCAD, Inventor, SolidEdge, CATIA, NX) programmer and a member of the Autodesk Developer Network. He is fluent in AutoLISP, VBA, and VB.NET. He has written for AugiWORLD and Inside AutoCAD (a monthly magazine that has since gone out of publication). You can contact him at sanganakskha@gmail.com.

### 问答

MKshrestha

August 14, 2011 at 10:23 am

How to create a situation to delete dimension automatically when the object is deleted. This question has no answer mentioned in this topic. Please, Kindly send me about this?

Sanjay Kulkarni

August 18, 2011 at 2:55 pm

I just realized that the above study is little in-complete. Here are the missing details:

You can specify the required point to create linear dimensions in three ways:

1 Select an object (using Object option).

2 Use one / two snap points. Remember, these snap points are 'attached' to the object.

3 Use one / two points ‘in the air' – not attached to any object – in the blank area of the drawing.

You will find that in case of options 2 and 3 above, the entity-list of the dimensions does not contain any reference to the reactor. And it all seems logical since no object is attached to the dimension. Snap points are only snap points – not the object.

I mentioned this fact since it may help us clarify an issue in one of the future posts.

Alex

September 6, 2011 at 12:37 pm

I can not figure out how to dimension properly in paperspace. I have been using ACAD since release 11 and have never had this problem.

When I create a viewport and try to dimension it, when I put a dimension on an item inside the viewport (even thought the viewport is not active) it gives me the dimension of the model, but if i was to select a centerline that was drawn in paperspace, it gives me a different dimension.

There used to be a simple way to set up the dimensioning so that you can pick on any item, entity, area etc. of the paperspace drawing (even inside the Viewport) and have no problem.

In the previous releases this was no problem, but now I can't get it to work.

Is there some new way to dimension paperspace, or do i have to create 2 dimension styles, one for paperspace items and one for model space items?

Thanks

Ellen

September 6, 2011 at 2:12 pm

I'm not aware that the procedure for dimensioning in paper space has changed. I have a post on it at https://allaboutcad.com/tutorials-dimension-in-paper-space/. Also, have you tried using annotative dimensions? See https://allaboutcad.com/tutorial-automate-annotation/.

Chuck Lowell

March 21, 2012 at 9:05 pm

I have a dimensioning problem. This problem arises it seems when I do a lot of deleting, moving, inserting, etc in a good size file. After I do a bunch of dimensioning and start to move things around, one or two of the dimensions points will either stay at home (original spot before moving) while I move an object and the other dim point will stretch to the new location, or two dim lines will fly off into a different part of the drawing for no apparent reason.

I do select the complete item I am moving. I talked to a local AUTOCAD rep. but she never seen/heard about it before. I have a NFS (Not for Sale )version of 2007.

Can anyone help? Thank You

Ellen

March 21, 2012 at 10:43 pm

I've never heard of that. Try asking in the Autodesk discussion groups.

im

May 16, 2012 at 10:21 pm

how i can run a lisp thath automatic delete associative dimension after i delete they elements?

## 20220308AutoLisp-Reactors.md

by Kenny Ramage

[Reactors - Part 1 | AfraLISP](https://www.afralisp.net/visual-lisp/tutorials/reactors-part-1.php)

[Reactors - Part 2 | AfraLISP](https://www.afralisp.net/visual-lisp/tutorials/reactors-part-2.php)

[Reactors - Part 3 | AfraLISP](https://www.afralisp.net/visual-lisp/tutorials/reactors-part-3.php)

3『

参考资料：

[AutoCAD 2021 Developer and ObjectARX 帮助 | Reactor Functions Reference (AutoLISP/ActiveX) | Autodesk](https://help.autodesk.com/view/OARX/2021/CHS/?guid=GUID-B83B512E-CEF6-43C5-9099-398999E254AF)

[Using Object Reactors - AutoCAD AutoLISP & Visual LISP Documentation](https://documentation.help/AutoCAD-ALISP-VLISP/WS73099cc142f4875516d84be10ebc87a53f-7c28.htm)

[AutoLISP Developer's Guide: Understanding Reactor Types and Events](http://docs.autodesk.com/ACD/2011/ENU/filesALG/WS73099cc142f4875516d84be10ebc87a53f-7c33.htm)

』

### Part 1

#### 1.1 What are Reactors?

"What the heck is a reactor?" I asked myself exactly the same question when I first came across the phrase. "It must be another name for an Event", I said to myself. I was wrong, again!

A reactor is an Object you attach to AutoCAD Objects to have AutoCAD notify your application when a particular event, or events occur. (Remember, that an AutoCAD Object can be an Object within your drawing, the drawing itself, or even AutoCAD, the application - just thought I'd tell you). This is the general procedure :

1 An Event occurs in your drawing.

2 AutoCAD notifies the Reaction associated with that Event.

3 The reaction then notifies your application, known as a callback function, passing to it certain information applicable to the Event in the form of arguments.

4 Your application then does it's thing using the information passed to it from the Reactor.

So, in simple terms, a Reactor is the "link" between the Event and the callback function.

1『这里对 Reactor 的定义真精辟：反应器是事件与回调函数的连接器。正巧前几天开发自动标注时知道了「关联标注」的概念，关联标注的自动更新就是靠的反应器来连接标注和被标注图形的。（2022-03-08）』

Before we get into looking at the different types of AutoCAD Reactors and Events, let's have a look at a fairly straightforward Reactor to give you an idea of how simple they can be. Before you start though, you must ask yourself two questions :

1 Before, after or during what Event do you want your function to occur?

2 What do you want your function to do?

For this example, I will answer both these questions on your behalf.

1 I want my function to run every time a drawing Save is completed.

2 I want the function to tell me what the name of the drawing is, and how big the drawing is.

O.K. Looking at the Reactor listings, (Refer to Visual Lisp Reference), under the Editor Reactor Types, I find a reactor called vl-dwg-reactor. This reactor, I am led to believe, responds to the "Save Complete" event of a drawing, and returns callback data of a string containing the actual file name used for the save. Hey, this is just what we are looking for. Let's order two to take-away…

1-3『

[AutoLISP Developer's Guide: Understanding Reactor Types and Events](http://docs.autodesk.com/ACD/2011/ENU/filesALG/WS73099cc142f4875516d84be10ebc87a53f-7c33.htm)

上面的官方链接里列出了所有的反应器类型，而且从这里知道了，直接用命令是可以查的。

```
(defun c:foo ()
  (vlr-types)
)
```

(:VLR-Linker-Reactor :VLR-Editor-Reactor :VLR-AcDb-Reactor :VLR-DocManager-Reactor :VLR-Command-Reactor :VLR-Lisp-Reactor :VLR-DXF-Reactor :VLR-DWG-Reactor :VLR-Insert-Reactor :VLR-Wblock-Reactor :VLR-SysVar-Reactor :VLR-DeepClone-Reactor :VLR-XREF-Reactor :VLR-Undo-Reactor :VLR-Window-Reactor :VLR-Toolbar-Reactor :VLR-Mouse-Reactor :VLR-Miscellaneous-Reactor :VLR-Object-Reactor)

』

Enough of this childish wit! Let's have a look at the reactor definition and syntax first :

vlr-dwg-reactor - Constructs an editor reactor object that notifies of a drawing event.

```
(vlr-dwg-reactor data callbacks)
```

Arguments :

data : AutoLisp data to be associated with the reactor object, or nil, if no data

callbacks : A list of pairs of the following form :

```
(event-name . callback_function)
```

where event-name is one of the symbols listed in the "DWG reactor events" table, and `callback_function` is a symbol representing the function to be called when the event fires. Each callback function will accept two arguments :

`reactor_object` - the VLR object that called the callback function,

list - a list of extra data elements associated with the particular event.

#### 1.2 How does it work?

I don't know about you? But, whew…! Right, let's try and put this into simple English. Let's look at the syntax again :

```
(vlr-dwg-reactor data callbacks)
```

The first part vlr-dwg-reactor is easy. This is the name of the reactor type. This name will be sent to your call back function.

The first argument data, is User Application Data. We usually set this to a reactor name of our choosing. This way we can distinguish between reactors if an Objects has multiple reactors attached.

The second argument callbacks is a straightforward list of dotted pairs.

1 The first element of the list is the name of the reactor "event" that will trigger the reactor and then call your callback function.

2 The second element, is the name of your Callback function.

This is what our reactor function will look like :

```
(vlr-dwg-reactor "Save Complete"  '((:vlr-savecomplete . savedrawinginfo)))
```

Or, graphically :

Let's have a look at our Reactor Function in action. Copy and Paste this coding into the Visual LISP Editor and save it as SaveDrawingInfo.lsp. Next Load the application, but do not run it.

```
;;----------------------------------------------------------------------------------
; setup and intilise the reactor
(vlr-dwg-reactor "Save Complete" '((:vlr-savecomplete . savedrawinginfo)))

;;----------------------------------------------------------------------------------
(defun saveDrawingInfo (calling-reactor commandInfo / reactInfo reactType reactData reactCall reactEvent reactCallback dwgname filesize)
  ; get the reactor Object
  (setq reactInfo calling-reactor
    ; get the reactor Type
    reactType (vl-symbol-name (vlr-type reactInfo))
    ; get the Application Data
    reactData (vlr-data reactInfo)
    ; get the Callback list
    reactCall (car (vlr-reactions reactInfo))
    ; extract the Event Reactor
    reactEvent (vl-symbol-name (car reactCall))
    ; extract the Callback Function
    reactCallback (vl-symbol-name (cdr reactCall))
  )
  ; get the Drawing Name
  (setq dwgname (cadr commandInfo)
    ; extract the filesize
    filesize (vl-file-size dwgname)
  )
  ;display the Drawing Name and Size
  (alert (strcat "The file size of " dwgname " is " (itoa filesize) " bytes."))
  ;Display the Reactor Information
  (alert
    (strcat
      "A " "\"" reactType "\"" " named " "\"" reactData "\"" "\n"
      "was triggered by a " "\"" reactEvent "\"" " event call." "\n"
      "Callback Data was passed to the" "\n"
      "\"" reactCallback "\"" " call back function."))
  (princ)
)
;;----------------------------------------------------------------------------------
```

1『

```
(vlr-dwg-reactor "Save Complete" '((:vlr-savecomplete . savedrawinginfo)))
```

放在 lisp 文件的最外层，确保该程序一直在跑。

意外之喜：自动保存也会触发 Reactor。解决了之前想要收集的工时数据问题。（2022-03-08）

借鉴上面的代码，自己写了一个 CAD 里任意新增一个实体对象即可触发 Reactor 的功能：

```
;;----------------------------------------------------------------------------------
(vlr-acdb-reactor "Object Appended" '((:vlr-objectAppended . objectAppendedinfo)))

(defun ObjectAppendedInfo (calling-reactor commandInfo / reactInfo reactType reactData reactCall reactEvent reactCallback dwgname filesize)
  ; get the reactor Object
  (setq reactInfo calling-reactor
    ; get the reactor Type
    reactType (vl-symbol-name (vlr-type reactInfo))
    ; get the Application Data
    reactData (vlr-data reactInfo)
    ; get the Callback list
    reactCall (car (vlr-reactions reactInfo))
    ; extract the Event Reactor
    reactEvent (vl-symbol-name (car reactCall))
    ; extract the Callback Function
    reactCallback (vl-symbol-name (cdr reactCall))
  )
  ;display the Drawing Name and Size
  (princ "\write data to file\n")
  (princ (car commandInfo))
  (princ "\n")
  (princ (cadr commandInfo))
  (princ)
)
;;----------------------------------------------------------------------------------
```

』

Once the application is loaded, return to AutoCAD and save the drawing. This dialog will appear :

Followed by this dialog :

Do you notice the calling-reactor and commandInfo argument declarations? This information, the Reactor Object Name and the Event Parameter Information is passed to our Callback Function from the Reactor. Clever hey?

In Part 2, we'll have a look at some Drawing Reactors.

### Part 2

#### 2.1 Drawing Reactors

Another Editor Type of reactor is the VLR-Command-Reactor. This reactor notifies us of a Command Event. In this instance we will make use of the vlr-commandEnded reactor event which returns a event parameter list containing a string identifying the command that has been cancelled. Every time a drawing is plotted, our call back function will first, save the drawing, and secondly save the drawing to a backup directory, namely C:/Backup. Let's have a look at the coding :

```
;;----------------------------------------------------------------------------------

(prompt " \nLoad Only....Do NOT Run...")
(vl-load-com)

;;----------------------------------------------------------------------------------
(vlr-command-reactor 
 "Backup After Plot" '((:vlr-commandEnded . endPlot)))

;;----------------------------------------------------------------------------------
(defun endPlot (calling-reactor endcommandInfo / thecommandend dwgName newname)
  (setq thecommandend (nth 0 endcommandInfo))
    (if (= thecommandend "PLOT")
      (progn
        (setq acadDocument (vla-get-activedocument (vlax-get-acad-object)))
        (setq dwgName (vla-get-name acadDocument))
        (setq newname (strcat "c:\\backup\\" dwgName))
        (vla-save acadDocument)
        (vla-saveas acadDocument newname)
      )
    )
  (princ)
)
```

A word of warning! Did you notice how I used ActiveX statements and functions to "Save" and "SaveAs". You cannot use interactive functions from within a reactor as AutoCAD may still be processing a command at the time the event is triggered. Therefore, avoid the use of input-acquisition methods such as getPoint, ensel, and getkword, as well as "selection set" operations and the command function.

Here's another interesting command event reactor :

When a user adds Text or Hatch to the drawing, the layer will automatically change to Layer "4" or Layer "6" respectively. When the command is completed, or cancelled, the user is returned to the original Layer he was on before he started the command. Save the file as LayMan.lsp, BUT please remember, that as this routine contains reactors, you must only Load it and NOT Run it. If you want to use this routine on a permanent basis, you'll have to ensure that it is loaded at startup. There is also no checking to ensure that the layers exist, are frozen, switched off, etc. and no other form of error checking. I've got to leave something for you to do!

```
(prompt " \nLoad Only....Do NOT Run...")
(vl-load-com)

;;----------------------------------------------------------------------------------
(vlr-command-reactor 
 nil '((:vlr-commandWillStart . startCommand)))
(vlr-command-reactor 
 nil '((:vlr-commandEnded . endCommand)))
(vlr-command-reactor 
 nil '((:vlr-commandCancelled . cancelCommand)))

;;----------------------------------------------------------------------------------
(defun startCommand (calling-reactor startcommandInfo / thecommandstart)
  (setq *OldLayer* (getvar "CLAYER"))
  (setq thecommandstart (nth 0 startcommandInfo))
  (cond
    ((= thecommandstart "TEXT") (setvar "CLAYER" "4"))
    ((= thecommandstart "MTEXT") (setvar "CLAYER" "4"))
    ((= thecommandstart "DTEXT") (setvar "CLAYER" "4"))
    ((= thecommandstart "HATCH") (setvar "CLAYER" "6"))
    ((= thecommandstart "BHATCH") (setvar "CLAYER" "6"))
  )
  (princ)
);defun
;;----------------------------------------------------------------------------------

(defun endCommand (calling-reactor endcommandInfo / thecommandend)
  (setq thecommandend (nth 0 endcommandInfo))
  (cond
    ((= thecommandend "TEXT") (setvar "CLAYER" *OldLayer*))
    ((= thecommandend "MTEXT") (setvar "CLAYER" *OldLayer*))
    ((= thecommandend "DTEXT") (setvar "CLAYER" *OldLayer*))
    ((= thecommandend "HATCH") (setvar "CLAYER" *OldLayer*))
    ((= thecommandend "BHATCH") (setvar "CLAYER" *OldLayer*))
  )
  (princ)
)

;;----------------------------------------------------------------------------------
(defun cancelCommand (calling-reactor cancelcommandInfo / thecommandcancel)
  (setq thecommandcancel (nth 0 cancelcommandInfo))
  (cond
    ((= thecommandcancel "TEXT") (setvar "CLAYER" *OldLayer*))
    ((= thecommandcancel "MTEXT") (setvar "CLAYER" *OldLayer*))
    ((= thecommandcancel "DTEXT") (setvar "CLAYER" *OldLayer*))
    ((= thecommandcancel "HATCH") (setvar "CLAYER" *OldLayer*))
    ((= thecommandcancel "BHATCH") (setvar "CLAYER" *OldLayer*))
  )
  (princ)
)
```

Did you notice that this application used three command reactors with three different command events. We could have incorporated all three reactor events and call back functions under one command reactor type, but I prefer to leave them separate for clarity and ease of debugging.

OK that's enough of command reactors. Let's have a look at Object Reactors. See you in Part 3.

### Part 3

#### 3.1 Object Reactors

Object Reactors, or VLR-Object-Reactor, fall under general reactor types. They are almost identical in functionality to Drawing and Command reactors except for a couple of things! They need to include a reference to the Object that will be reacted upon, (Crikey, that sounds terrible!) and the reference to the Object needs to be created before the reactor is called. Let's have a look at the syntax of an Object reactor :

```
(vlr-object-reactor owners data callback)
```

The data and callback arguments, we are familiar with. But what is the owners argument? This is an AutoLISP list of Visual Lisp Objects identifying the drawing Objects to be watched. In other words, a reference to the Object that contains the reactor.

The reactor event we are going to use is the :vlr-modified event, and our Callback function will be named print-length. Have a look at the coding for our reactor :

```
(vlr-object-reactor (list myLine) "Line Reactor"
  '((:vlr-modified . print-length)))
```

As I mentioned earlier though, we need to have a reference to the Object before we can call this statement. Consider the following :

```
(vl-load-com)

;;---------------------------------------------------------------------
(defun line-draw (/ acadDocument mspace apt pt myLine lineReactor)
  (setq acadDocument (vla-get-activedocument (vlax-get-acad-object)))
  (setq mspace (vla-get-modelspace acadDocument))
  (setq apt (getpoint "Specify First Point: "))
  (setq pt (getpoint "Specify next point: " apt))
  (setq myLine (vla-addline mspace (vlax-3d-point apt)(vlax-3d-point pt)))
  (setq lineReactor (vlr-object-reactor (list myLine)
  "Line Reactor" '((:vlr-modified . print-length))))
  (princ)
)
```

We started off by drawing a line. As the line was created from scratch, and created using Visual LISP functions, we already have a reference to the line Object (myLine). We can now safely run our reactor function and attach it to our Line. "But where is the Callback function?"

1『感觉这里的思想跟关联标注类似，生成的标注以及原图形里都增加了 Reactor 对象。（2022-03-08）』

Hah, I was waiting for that. We've made the Callback function a separate function for one main reason. If we didn't, every time we ran the application it would prompt us to draw a new line. So, what we have to do now, is link the reactor function to our Callback function so that when our line is modified, only the Callback function is put into motion. The reactor sends three arguments to the Callback function, the notifier-object (our line), the reactor-object (:vlr-modified), and the event parameter-list which in this case is nil.

Here's the coding for the Callback function :

```
(defun print-length (notifier-object reactor-object parameter-list)
  (cond
    ((vlax-property-available-p notifier-object "Length")
    (alert (strcat "The length is now "
                          (rtos (vla-get-length notifier-object)))))
  )
  (princ)
)
```

Copy all of this coding into one file and save it as Line-Draw.lsp. Now load Line-Draw.lsp and then run the line-draw function. Draw a single line when prompted. Now stretch the line so that it's length changes. A dialog will appear displaying the new length of the line :

AutoCAD Message

In essence, this is what happened :

1 We loaded Line-Draw.lsp and all functions contained within were placed into memory.

2 We ran the line-draw function which prompted us to draw a line. The reactor was then loaded and linked to both the line Object and the Callback function.

3 As the Callback function print-length was also loaded into memory, every time we modify the line Object, the Callback function is processed and the length of the line is displayed.

Did you notice how we checked that our Object had a Length Property before continuing? Good idea, as this validation can save lot's of problems.

"But what happens when I close my drawing? Will I lose all my reactors?" Good questions. Reactors can be transient or persistent. Transient reactors are lost when the drawing closes and this is the default reactor mode. Persistent reactors are saved with the drawing and exist when the drawing is next open.

1『关键信息来了，Reactor 分为暂时性的和永久性的，类似于 entityname 和 entityhandle，而且可以用函数 vlr-pers 把临时性的改为永久性的，用函数 vlr-pers-release 把永久性的改为临时性的。（2022-03-05）』

You can use the vlr-pers function to make a reaction persistent. To remove a persistence from a reactor and make it transient, use the vlr-pers-release function. To determine whether a reactor is persistent or transient, use the vlr-pers-p function. Each function takes the reactor Object as it's only argument :

```
_$(vlr-pers lineReactor)
#<VLR-Object-Reactor>
```

If successful vlr-pers returns the specified reactor Object.

Note : A reactor is only a link between an event and a Callback function. The Callback function is not part of the reactor, and is normally not part of the drawing. The reactors saved in the drawing are only usable if their associated Callback functions are loaded in AutoCAD. In other words, if we made our reactor lineReactor persistent, we would have to ensure that the Callback function print-length was loaded every time the drawing containing our lines with reactors was opened.

If you would like the source coding for all the examples in this tutorial, then you can download them from here.

2『已下载源码「20220308AutoLisp-Reactors」作为该碎片文章的附件。（2022-03-08）』

## 20220310CAD二次开发介绍.md

基本概念：

1、AutoCAD 开发选择，ObjectARX 还是 .net API。

2017-08-15 在网上找的二者区别的记录如下：

因为 cad 是用 cpp 写的，所以 cpp 提供更多的接口，提供了自定义对象，可以比 .net 实现更多的功能。

企业级的开发建议用 cpp，如果是个人使用，用 .net 开发效率更高，学习成本也更低。

ObjectARX 支持自定义实体（自定义新的几何图形），.net 不支持自定义实体；ObjectARX 编译比 .net 难些，但 .net 开发效率会比 arx 高些，其他的和 .net 差不多了，.net API 基本上就是封装的 ARX 的一些函数。

2015.05.01 上传到网上的一份资料如下：

本文基于 AutoCAD 2006 新推出的 .NET API 为工具，介绍了在 .NET 平台下对 AutoCAD 进行二次开发的技术，并与目前常用的 VBA、ObjectARX 作了对比。同时讨论了如何弥补 .NET API 某些不足的功能；

当前 AutoCAD 的二次开发工具主要有：VisualLisp、VBA 和 ObjectARX 等。其中，VisualLisp 与 VBA 较为简单，特别是 VBA，使用方便且开发速度较快，但其功能相比 ObjectARX 有所不足，尤其是对面向对象的功能支持不好。而 ObjectARX 基于 VC 平台，在 C++ 的支持下，其功能非常强大，可以很好地运用各种面向对象技术，但其缺点是发开速度比较慢，同时对开发人员的能力要求较高。

.NET 是微软新推出的开发平台，具有众多优点。基于.NET 平台对 AutoCAD 进行二次开发，可充分利用 .NET 的各种优势，在保证功能强大的前提下大大提高开发速度。

NET API 简介：

在新推出的 AutoCAD 2006 中，Autodesk 为其开发增加了 .NET API。.NET API 提供了一系列托管的外包类（Managed Wrapper Class），使开发人员可在 .NET 框架下，使用任何支持 .NET 的语言，如 VB.NET、C# 和 Managed C++ 等对 AutoCAD 进行二次开发。其优点是完全面向对象，在拥有与 C++ 相匹配的强大功能的同时，具有方便易用的特点，是较理想的 AutoCAD 二次开发工具。

.NET API 与传统 ObjectARX 的主要区别：

.NET API 与传统 ObjectARX 的区别主要源于在 .NET 环境下开发应用程序与在 VC 环境下开发应用程序的区别。首先，在 VC 环境下，程序员需要自己管理内存的申请和释放，而 .NET 采用了垃圾回收机制，由 .NET 框架自行判断内存回收的时机并实行回收，从而解决了令 C++ 程序员头痛的内存泄漏问题。也正是由于这个特点，在 .NET 环境下不能象在 C++ 环境下那样利用析构函数释放其他的资源，需要程序员在程序中显式地释放。在 .NET API 中，主要通过 Dispose 来函数进行资源的释放。

其次，ObjectARX 中的各种反应器（Reactor）在 .NET API 中由外包类映射为各种事件（Event），可通过定义这些事件的响应函数来响应 AutoCAD 的各种操作。同时对于错误信息的处理也从函数返回值改变为通常异常来处理，使其更好地兼容 .NET。由于 VB.NET、C# 等语言都是完全面向对象的，没有全局函数的概念，所以 .NET API 将 ObjectARX 下的全局函数封装为 .NET API 下的某些对象或对象的属性，如 ObjectARX 下与用户交互的系列全局函数被封装为 CommandLinePrompt 类。

说明：

.NET API 又是怎么工作的呢？我们都知道 ACAD 的功能其实是通过 ObjectARX 来实现的，所以 .NET API 其实就通过 VC++ 和 C++/CLI（原来的 VC++.NET）混合编程把 ARX 功能包装一下以 .NET 类的形式展示出来。也就是说使用 .NET API 其实使用的是 ARX，而 .NET API 能实现多少功能就取决于包装的程度，也就是包装了多少 ARX 的功能。了解 ARX 的朋友都知道，使用 ARX 库，由于 C++ 语言的特点，我们经常需要派生 ARX 类。但使用 .NET 就不必了，我们经常只需对对应的 event 来个 delegate 就行了，相比而言简洁得一塌糊涂（比如：Autodesk.AutoCAD.ApplicationServices.Application.BeginQuit 等 event）。根据前边对自定义对象的描述，现在就清楚了，.NET API 除了部分自定义对象功能外，基本能代替 ARX。